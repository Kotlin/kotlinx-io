/*
 * Copyright 2016-2018 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

configurations {
    testArtifacts
}
kotlin {
    sourceSets {
        jvmMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

                api rootProject
                api project(':kotlinx-coroutines-io')

                api 'org.ow2.asm:asm-debug-all:5.2'
                api 'com.google.code.gson:gson:2.6.2'
            }
        }
        jvmTest {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
                api "junit:junit:4.12"
            }
        }
    }
}

dependencies {
    testArtifacts rootProject
    testArtifacts project(':kotlinx-coroutines-io')
}

jvmTest {
    dependencies {
    }

    def modules = configurations.testArtifacts.dependencies.findAll { it instanceof ProjectDependency }.collect {
        it.dependencyProject.projectDir
    }

    def artifactNames = configurations.testArtifacts.dependencies.findAll { it instanceof ProjectDependency }.collect {
        it.dependencyProject.name
    }

    systemProperty 'validator.input.modules', modules.join(File.pathSeparator)
    systemProperty 'validator.input.artifactNames', artifactNames.join(File.pathSeparator)
    systemProperty 'overwrite.output', project.properties['overwrite.output']
    jvmArgs '-ea'
}
