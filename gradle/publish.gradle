// Configures publishing of Maven artifacts to Bintray

apply plugin: 'maven'
apply plugin: 'maven-publish'

apply from: project.rootProject.file('gradle/pom.gradle')

// ------------- tasks
task stubSources(type: Jar) {
    classifier = 'sources'
}

task stubJavadoc(type: Jar) {
    classifier = 'javadoc'
}

task emptyJar(type: Jar) {
}

publishing {
    repositories {
        maven {
            def user = 'kotlin'
            def repo = 'kotlinx'
            def name = 'kotlinx.io'
            url "https://api.bintray.com/maven/$user/$repo/$name/;publish=0"
            credentials {
                username = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : System.getenv('BINTRAY_USER') ?: ""
                password = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY') ?: ""
            }
        }
        maven {
            name = "testLocal"
            url = "$rootProject.buildDir/m2"
        }
    }
    publications.all {
        pom.withXml(configureMavenCentralMetadata)

        def type = it.name
        switch (type) {
            case 'kotlinMultiplatform':
                task sourcesJar(type: Jar) {
                    classifier 'sources'
                    from kotlin.sourceSets.commonMain.kotlin
                }

                it.artifactId = "$project.name-native"
                it.artifact emptyJar
                it.artifact stubJavadoc
                it.artifact sourcesJar
                break
            case 'metadata':
                it.artifactId = "$project.name"
                break
            case 'jvm':
            case 'js':
            case 'native':
                it.artifactId = "$project.name-$type"
                break
        }
    }

    kotlin.targets.all { target ->
        def publication = publishing.publications.findByName(target.name)

        if (publication != null) {
            publication.artifact stubJavadoc

            if (target.platformType.name != 'native') {
                publication.moduleDescriptorGenerator = null
            } else {
                publication.artifact emptyJar
            }
        }
    }
}

publish.dependsOn publishToMavenLocal
